group 'com.edibleday'

ext {
    teavmVersion = '0.5.1'
}

buildscript {
    ext {
        kotlin_version = '1.2.21'
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


apply plugin: 'kotlin'
apply plugin: 'maven-publish'

version = '0.4.3.3'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.teavm:teavm-core:${teavmVersion}"
    compile "org.teavm:teavm-tooling:${teavmVersion}"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.apache.maven:maven-plugin-api:3.3.3'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    from "AUTHORS"
    from "LICENCE"
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    from "AUTHORS"
    from "LICENCE"
}



publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId 'teavm-gradle-plugin'
            version project.version
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
